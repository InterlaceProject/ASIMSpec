CoreASIM server

use TurboASMPlugin
use ConditionalRule
use Standard
use TimePlugin

init Start

policy whileRunning = skip
scheduling whileRunning

universe ACCOUNT_TYPES = { CC, DOMU, MIRROR, Income, Prepaid, Bisoo, Topup }
universe OPERATION = { credit, debit }
universe UNIT = {SRD, EUR}
universe USER_TYPE_GROUP = { 
								welcome,
								retail,
								company,
								full,
								employee,
								on_hold,
								MNGR,
								consumer,
								consumer_verified
							}
universe CHANNEL = { Service, POS }

universe MESSAGE_REQUESTS = {
								CreditPreviewReq,
								CreditPerformReq,
								DebitPreviewReq,
								DebitPerformReq,
								DebitAckCompletion,
								ProceedMessage,
								DoNotProceedMessage,
								TransferPerformed,
								ConfirmationReq
							}

universe ACCOUNT_ID = {accId1, accId2, accId3, accId4, accId5, accId6, accId7}
universe MEMBER_ID = {mbrId1, mbrId2, mbrId3, mbrId4}

controlled profileTable: MEMBER_ID -> MAP
controlled accountTable: ACCOUNT_ID -> MAP
controlled userGroupTable: MEMBER_ID -> USER_TYPE_GROUP
controlled TT: OPERATION * UNIT * USER_TYPE_GROUP -> SET
controlled AccT: OPERATION * UNIT * ACCOUNT_TYPES -> SET
controlled sessionData: MAP

controlled Ledger: MAP
controlled PendingTransactions: MAP

universe TRANSACTION_STATUS = {
								TRANSACTION_PENDING,
								TRANSACTION_PERFORMED,
								TRANSACTION_REJECTED
							}

controlled lifetimeForOTPs: INTEGER

//-----------------------------------------------------------------------------------------------------------
//				begin tiny logger (because debug logger of ASM-Plugin is laborious to use)
//-----------------------------------------------------------------------------------------------------------
universe LOG_LEVEL = {FATAL, ERROR, WARN, INFO, DEBUG} //all log levels
controlled current_log_level: LOG_LEVEL //current log leve
controlled log_states: SET  //list of states which should be written to the log

rule initCustomLogger(set_log_level) = seq
	//TODO: add log to file option?
	current_log_level := set_log_level
	log_states := {FATAL}
	
	if set_log_level = ERROR then
		log_states := {FATAL, ERROR}
	if set_log_level = WARN then
		log_states := {FATAL, ERROR, WARN}
	if set_log_level = INFO then
		log_states := {FATAL, ERROR, WARN, INFO}
	if set_log_level = DEBUG then
		log_states := {FATAL, ERROR, WARN, INFO, DEBUG}
	
	do_log("LOG_LEVEL: " + current_log_level + ", LOG STATES: " + log_states, DEBUG)
endseq

rule do_log(text, level) = local write_log in seq
	//strangely "= true" needs to be placed
	//otherwise the if-statement fails
	if (level memberof log_states) = true then
		print level + ": " + text
endseq

//-----------------------------------------------------------------------------------------------------------
//				init
//-----------------------------------------------------------------------------------------------------------
rule Start = {
	do_log("ASIM MUTUAL CREDIT server init", INFO)
	
	Ledger := {->}
	PendingTransactions := {->}
	lifetimeForOTPs := 100000000000
	
	//defined in initaccounts.casim
	InitData()
	
	//initalize logger
	initCustomLogger(DEBUG)
	
	program(self) := DispatchMessages
}

//-----------------------------------------------------------------------------------------------------------
//				begin messaging rules
//-----------------------------------------------------------------------------------------------------------

derived messageContent(msg) =
	return content in
		content := nth(msg, 1)

derived messageSubject(msg) =
	return ms in
		ms := nth(msg, 2)
		
//put message together with subject
derived AssembleMessage(msg, sub) =
	return message in
		message := [msg, sub]

// send message to receipient mbr
rule Send(msg, agent) = {
	send messageContent(msg) to agent with subject messageSubject(msg)
}

//-----------------------------------------------------------------------------------------------------------
//				begin message assembling
//-----------------------------------------------------------------------------------------------------------

// not permitted message
derived NotPermitted(transfer, error) =
	return message in
		message := AssembleMessage(error + " for transfer: " + transfer, toString(DoNotProceedMessage))

//prepare normal message
derived YouMayProceedWith(transfer) =
	return message in
		message := AssembleMessage(transfer, toString(ProceedMessage))

//success message with optional limitAlert
derived TransferPerformedSuccessfully(transfer, limitAlert) =
	return message in
		if limitAlert = undef then
			message := AssembleMessage(transfer + " performed successfully", toString(TransferPerformed))
		else
			message := AssembleMessage(transfer + " performed successfully with alert " + limitAlert,
						toString(TransferPerformed))

//create confirmation for debit request
derived ConfirmationRequest(transfer) =
	return message in
		message := AssembleMessage(transfer, toString(ConfirmationReq))

//message saying that otp is incorret or outdated
derived IncorrectOtpFor(otp, amount, creditor) =
	return message in
		message := AssembleMessage(
			"given otp: " + otp + " is exired or incorrect for amount " + amount + " to " + creditor, 
			toString(DoNotProceedMessage))

//"not account owner" - message 
derived NotAccountOwner(mbr, accId) =
	return message in
		message := mbr + " is not owner of account " + accId

//source group not correct
derived SourceGroupViolation(mbr) =
	return message in
		message := mbr + " in group " + groupOf(mbr) + " does not have the right privilegedes for that transfer" 

//destination group not correct
derived DestinationGroupViolation(mbr, tt) =
	return message in
		message := "mbr is in group " + groupOf(mbr) + "but needs to be in one of these " + tt

//account connectivity not defined
derived SourceAccountViolation(fromA) =
	return message in
		message := "source account " + fromA + " not of the correct type"

// account not connected
derived DestinationAccountViolation(fromA, ac) =
	return message in
		message := fromA + "account " + fromA + " is not in one of these groups " + ac

//LowBalanceAlert
derived LowBalanceAlert =
	return message in
		message := "LowBalanceAlert"

//HighBalanceAlert
derived HighBalanceAlert =
	return message in
		message := "HighBalanceAlert"
		
//HighVolumeAlert
derived HighVolumeAlert =
	return message in
		message := "HighVolumeAlert"
		
//CapacityViolation
derived CapacityViolation =
	return message in
		message := "CapacityViolation"

//AvailBalanceViolation
derived AvailBalanceViolation =
	return message in
		message := "AvailBalanceViolation"

//UpperLimitViolation
derived UpperLimitViolation =
	return message in
		message := "UpperLimitViolation"
		
//OK
derived OK =
	return message in
		message := "OK"

//units are different - necessary?
derived UnitsMismatch(fromA, toA) =
	return message in
		message := "Unit mismatch: from account " + fromA + " has unit " + unitOf(fromA) + " and to account " + toA + " has unit " + unitOf(toA)

//-----------------------------------------------------------------------------------------------------------
//				begin globally used rules
//-----------------------------------------------------------------------------------------------------------

//read value of a map (amap) identified by a key
derived v(amap, key) =
	return ret in
		if amap = undef or key = undef then
			ret := undef
		else
			choose v in amap with key = nth(v, 1) do ret := nth(v, 2)

//check the balances
//maybe derived function instead of returning rule
//amount condition check missing (D2.1)
derived AccountLimitsCheck(fromAccount, toAccount, amount) =
	return message in
		if (fromAccount = undef or toAccount = undef or amount = undef) then
			message := "Message format incorrect"
		else
			if CanBeSpentBy(fromAccount, amount) then
				if CanBeCashedBy(toAccount, amount) then
					if HasSellCapacityFor(toAccount, amount) then
						message := OK()
					else
						message := CapacityViolation()
					endif
				else 
					message := UpperLimitViolation()
				endif
			else
				message := AvailBalanceViolation()
			endif
		endif

derived CanBeSpentBy(fromAcc, amount) =
	return spendable in
		spendable := (availableBalance(fromAcc) >= amount)
 
derived CanBeCashedBy(toAcc, amount) =
	return cashable in
		cashable := ((balance(toAcc) + amount) <= upperLimit(toAcc))
		
derived HasSellCapacityFor(toAcc, amount) =
	return sellable in
		sellable := (amount <= availableCapacity(toAcc))

//get balance for account	
derived balance(acc) =
	return bal in
		bal := v(acc, "balance")
		
//get account upper limit
derived upperLimit(acc) =
	return ul in
		ul := v(acc, "upperLimit")
		
//get credit limit
derived creditLimit(acc) =
	return cl in
		cl :=  v(acc, "creditLimit")
		
//get from account
derived fromAcc(transfer) =
	return fa in
		fa :=  v(transfer, "from") 

//get from account
derived toAcc(transfer) =
	return fa in
		fa :=  v(transfer, "from") 

//return amount
derived amountOf(transfer) =
	return a in
		a := v(transfer, "amount")
		
//get debitor member id of a transfer
derived debitorOf(transfer) =
	return mbrId in
		mbrId := v(transfer, "debitor")
		
//get creditor member id of a transfer
derived creditorOf(transfer) =
	return mbrId in
		mbrId := v(transfer, "creditor")

//determine otp
derived otpOf(transfer) =
	return otp in
		otp := v(transfer, "otp")

//calualte avaiable balance by using the account
derived availableBalance(account) =
	return availBal in
		availBal := balance(account) + creditLimit(account)

//calualte avaiable balance by using the account
derived availableCapacity(toAcc) =
	return capacity in
		capacity := 100000000000000000 //TODO!!!

//get account set by id		
derived accountOf(accId) =
	return ac in
		ac := accountTable(accId)

//get unit of account id
derived unitOf(accId) =
	return unit in
		unit := v(accountOf(accId), "unit")

//get account type of account id
derived accountType(accId) =
	 return type in
	 	type := v(accountOf(accId), "accountType")

//get owner of account
derived ownerOf(accId) =
	return owner in
		owner := v(accountOf(accId), "MemberID")

//get profile of member
derived profileOf(mbrId) =
	return profile in
		profile := profileTable(mbrId)

//get group of member by id
derived groupOf(mbrId) =
	return group in
		group := userGroupTable(mbrId)

//get member id by using session information
derived activeLogin(client_address) =
	return mbrId in
		forall session in sessionData do
			if toString(client_address) = nth(session, 1) then
				mbrId := nth(session, 2)
			
//get client id by using session information
derived activeClient(login_mbrId) =
	return client in
		choose session in sessionData with login_mbrId = nth(session, 2) do
				client := nth(session, 1)

derived memberAccounts(profile) =
	return aL in
		aL := profile("accounts")

//status of transaction
derived status(transaction) =
	return s in
		s := v(transaction, "state")

//get credit account
derived creditAccount(mbrId) =
	return accId in
		if memberAccounts(profileOf(mbrId)) = undef then
			accId := undef
		else
			forall acc in memberAccounts(profileOf(mbrId)) do
				if ownerOf(acc) = mbrId and accountType(acc) = "CC"  then
					accId := acc
				
//fake transaction id
derived NewTransactionID(len) =
	return tID in
		tID := HexString(len)
	
//one time pad generation
derived OneTimePassword(strength) =
	return otp in
		otp := HexString(strength)
		
//generate a random hex string with given length
derived HexString(len) =
	return tID in seq
		tID := ""
		while (|tID| < len) tID := tID + RandomHex()
	endseq

//get random hex number
derived RandomHex =
	return hex in
		choose n in ["0", "1", "2", "3", "4", 
					 "5", "6", "7", "8", "9", 
					 "a", "b", "c", "d", "e", "f"] do hex := n

//read transaction id
derived idOf(trans) =
	return id in
		id := v(trans, "id")

//create transaction for ledger
derived Transaction(transfer, mbr, operation, date) =
	return t in
		t := {
			"transfer" -> transfer,
			"date" -> date,
			"member" -> mbr,
			"operation" -> operation
		}		

//append transaction
rule Append(trans, ledger) = 
local transactionId in seq
	transactionId := NewTransactionID(16)
	add { transactionId -> trans } to ledger
	
	do_log(ledger, DEBUG)
endseq

//check for "small" amount
derived Small(amount) =
	return small in
		small := (amount < 100)

//test if an one time pad has been expired		
derived Expired(otp) = 
	return expired in
		expired := ((now - birthtime(otp)) > lifetimeForOTPs) //TODO

//get time when otp has been created
derived birthtime(otp) =
	return time in
		time := v(v(PendingTransactions, otp), "created")

//check alert limits
//  checked after current transfer has
//  been performed on the ledger
derived CheckAccountLimitsAlerts(profile, account) =
	return alert in
		if ((creditLimit(account) + balance(account)) < v(profile, "lowBalanceAlert")) then
			alert := LowBalanceAlert()
		else if ((upperLimit(account) - balance(account)) < v(profile, "highBalanceAlert")) then
			alert := HighBalanceAlert()
		else if ((v(profile, "capacity") - v(profile, "saleVolume")) < v(profile, "highVolumeAlert")) then
			alert := HighVolumeAlert()
		else 
			alert := undef
			
// check for a credit request
// might be party separated for the debit check (similar requirements?)
// Credit/DebitTypeCheck
rule PreviewCheck(mbr, fromA, toA, fromGrpId, toGrpId, operation) =
local errormessage, tt, ac in seq
	errormessage := undef //TODO create message from derived function
	
	// checks begin	
	if (unitOf(fromA) = unitOf(toA)) then seq // unit check - optional
		do_log("units are equal", DEBUG)
		
		tt := TT("credit", unitOf(fromA), fromGrpId)
		do_log("TT -> " + tt, DEBUG)
		
		if (mbr != ownerOf(fromA)) then
			errormessage := NotAccountOwner(mbr, fromA)
		else if (tt = undef) then //replaces MayStartCredit/DebitOpns
			errormessage := SourceGroupViolation(mbr)
		else if (toGrpId memberof tt) then seq //TT check
			do_log("group in TT", DEBUG)
			
			ac := AccT(operation, unitOf(fromA), accountType(fromA)) 
			do_log("AC -> " + ac, DEBUG)
			
			if (ac = undef) then
				errormessage := SourceAccountViolation(fromA)
			else if (accountType(toA) memberof ac) then //account connectivity test
				do_log("accounts compatible", DEBUG)
			else errormessage := DestinationAccountViolation(fromA, ac)
		endseq
		else errormessage := DestinationGroupViolation(mbr, tt)
	endseq
	else errormessage := UnitsMismatch(fromA, toA)
	
	result := errormessage
endseq

//-----------------------------------------------------------------------------------------------------------
//				begin CreditPreviewReq
//-----------------------------------------------------------------------------------------------------------

rule HandleCreditPreviewReq(transfer, client) = 
	local error in seq
	do_log(client + " -> CreditPreviewReq received: " + transfer, INFO)
	
	//do check the transfer if is ok
	error <- PreviewCheck(
		activeLogin(client),
		fromAcc(transfer),
		toAcc(transfer),
		groupOf(ownerOf(fromAcc(transfer))),
		groupOf(ownerOf(toAcc(transfer))),
		"credit")
	
	//communicate status
	//if seq block is removed send is not performed
	if (error = undef) then seq
		Send(YouMayProceedWith(transfer), client)
	endseq 
	else seq
		Send(NotPermitted(transfer, error), client)
	endseq
endseq

//-----------------------------------------------------------------------------------------------------------
//				begin CreditPerformReq
//-----------------------------------------------------------------------------------------------------------
rule HandleCreditPerformReq(transfer, client) = 
local limitAlert, bC, error in seq
	do_log(client + " -> CreditPerformReq received: " + transfer, INFO)	
	
	//do check the transfer if still ok
	error <- PreviewCheck(
		activeLogin(client),
		fromAcc(transfer),
		toAcc(transfer),
		groupOf(ownerOf(fromAcc(transfer))),
		groupOf(ownerOf(toAcc(transfer))),
		"credit")

	if (error != undef) then seq
		Send(NotPermitted(transfer, error), client)
	endseq
	else seq
		bC := AccountLimitsCheck(
			accountOf(fromAcc(transfer)),
			accountOf(toAcc(transfer)),
			amountOf(transfer))

		if bC = "OK" then seq
		
			do_log("balance check passed: performing credit transfer!", INFO)		
			
			//finally performing transfer by appending to the current ledger
			Append(Transaction(transfer, client, "credit", now), Ledger)
			
			limitAlert := CheckAccountLimitsAlerts(
				profileOf(ownerOf(fromAcc(transfer))),
				accountOf(fromAcc(transfer)))
			
			if (limitAlert = undef) then
				do_log("No limit alert", DEBUG)
			else
				do_log("Limit alert: " + limitAlert, WARN)
		
			//send success message, attach limitAlert if necessary
			Send(TransferPerformedSuccessfully(transfer, limitAlert), client)	
			skip //dummy to get message sent
		endseq
		else seq
			Send(NotPermitted(transfer, "Balance check problem: " + bC), client)
		endseq
	endseq
	
endseq

//-----------------------------------------------------------------------------------------------------------
//				begin DebitPreviewReq
//-----------------------------------------------------------------------------------------------------------
rule HandleDebitPreviewReq(transfer, client) = seq
	//get login information
	creditor := activeLogin(client)
		
	do_log(creditor + " -> DebitPreviewReq received: " + transfer, INFO)
	do_log(creditor + " -> login name: " + creditor, DEBUG)
	do_log(creditor + " -> profile: " + profileOf(creditor), DEBUG)
	do_log(creditor + " -> accounts: " + memberAccounts(profileOf(creditor)), DEBUG)
	do_log(creditor + " -> creditAccount: " + creditAccount(creditor), DEBUG)
	
	//debitor=buyer 
	fromA := creditAccount(debitorOf(transfer))
	
	//creditor=seller
	toA := creditAccount(activeLogin(client))
	do_log("from Account:: "+ fromA, DEBUG)
	do_log("to Account::" + toA, DEBUG)
	
	//do check the transfer if still ok
	error <- PreviewCheck(
		debitorOf(transfer),
		fromA,
		toA,
		//fromGroup and toGroup are both from the seller/creditor
		groupOf(creditor),
		groupOf(creditor),
		"debit")
	
	do_log("PreviewCheck:: " + error, DEBUG)
	
	//communicate status
	//if seq block is removed send is not performed
	if (error = undef) then seq
		Send(YouMayProceedWith(transfer), client)
	endseq 
	else seq
		Send(NotPermitted(transfer, error), client)
	endseq
endseq

//-----------------------------------------------------------------------------------------------------------
//				begin DebitPerformReq
//-----------------------------------------------------------------------------------------------------------
rule HandleDebitPerformReq(transfer, client) = seq
	do_log(client + " -> DebitPerformReq received:: " + transfer, INFO)
	
	//debitor=buyer 
	fromA := creditAccount(debitorOf(transfer))
	//get login information
	creditor := activeLogin(client)
	//creditor=seller
	toA := creditAccount(activeLogin(client))
	
	do_log(creditor + " -> login name: " + creditor, DEBUG)
	do_log(creditor + " -> profile: " + profileOf(creditor), DEBUG)
	do_log(creditor + " -> accounts: " + memberAccounts(profileOf(creditor)), DEBUG)
	do_log(creditor + " -> creditAccount: " + creditAccount(creditor), DEBUG)
	
	do_log("from Account:: "+ fromA, DEBUG)
	do_log("to Account::" + toA, DEBUG)
	
	//re-do check the transfer if still ok
	error <- PreviewCheck(
		debitorOf(transfer),
		fromA,
		toA,
		//fromGroup and toGroup are both from the seller/creditor
		groupOf(creditor),
		groupOf(creditor),
		"debit")
	
	do_log("PreviewCheck:: " + error, DEBUG)
	
	if (error != undef) then seq
		do_log("error:: " + error, DEBUG)
		Send(NotPermitted(transfer, error), client)
	endseq
	else seq
		bC := AccountLimitsCheck(accountOf(fromA), accountOf(toA), amountOf(transfer))		
		
		//do_log("blanace from:: " + balance(accountOf(fromA)), DEBUG)
		//do_log("blanace to:: " + balance(accountOf(toA)), DEBUG)
		do_log("bC:: " + bC, DEBUG)
		
		if bC = "OK" then seq
		
			if Small(amountOf(transfer)) then seq
				do_log("balance check passed: performing debit transfer for small amount!", INFO)		
		
				//finally performing transfer by appending to the current ledger
				Append(Transaction(transfer, client, "debit", now), Ledger)
				
				limitAlert := CheckAccountLimitsAlerts(
				profileOf(ownerOf(fromA)),
				accountOf(fromA))
			
				if (limitAlert = undef) then
					do_log("No limit alert", DEBUG)
				else
					do_log("Limit alert: " + limitAlert, WARN)
					
				//send success message, attach limitAlert if necessary
				Send(TransferPerformedSuccessfully(transfer, limitAlert), activeClient(creditor)) //send to creditor
				Send(TransferPerformedSuccessfully(transfer, limitAlert), activeClient(debitorOf(transfer))) //send to debitor
			endseq
			else seq
				do_log(client + " -> debit creating pending transaction", DEBUG)
				
				Insert(transfer, creditor, PendingTransactions)
				
				Send(ConfirmationRequest(transfer), activeClient(debitorOf(transfer)))
				skip //dummy command to get the message sent
			endseq
		endseq
		else seq
			Send(NotPermitted(transfer, "Balance check problem: " + bC), client)
		endseq
	endseq
endseq

rule Insert(transfer, creditor, pendingTransactions) = local otp in seq
	otp := OneTimePassword(20)
	
	//add missing information
	add { "otp" -> otp} to transfer
	add { "creditor" -> creditor } to transfer
	add { "created" -> now } to transfer
	add { "state" -> TRANSACTION_PENDING } to transfer
	
	//add the pending transfer to the pending transactions
	add { otp -> transfer } to pendingTransactions
endseq

//-----------------------------------------------------------------------------------------------------------
//				begin DebitPerformReq
//-----------------------------------------------------------------------------------------------------------
rule HandleDebitAckCompletion(transfer, client) = local otp, transaction in seq
	do_log(client + " -> DebitAckCompletion received" + transfer, INFO)
	//!important
	//all internal checks and actions we need to use transaction
	
	otp := otpOf(transfer)
	transaction := v(PendingTransactions, otp)
	
	do_log("OTP: " + otp, DEBUG)
	do_log("trans: " + transaction, DEBUG)
	do_log("status: " + status(transaction), DEBUG)
	
	if Expired(otp) or (transaction = undef) or (status(transaction) != TRANSACTION_PENDING) then
		Send(
			IncorrectOtpFor(otp, amountOf(transfer), creditorOf(transfer)),
			client) //client => where debitor is logged in
	else local fromA, toA, bC in seq
		//taking transaction from pending transactions
		//instead of tansfer which was sent from client
				
		//debitor=buyer 
		fromA := creditAccount(debitorOf(transaction))
		//creditor=seller
		toA := creditAccount(creditorOf(transaction))
		bC := AccountLimitsCheck(accountOf(fromA), accountOf(toA), amountOf(transaction))
				
		if bC != OK() then seq
			Send(NotPermitted(transfer, "Balance check problem: " + bC), client)
			//update state
			add { "state" -> TRANSACTION_REJECTED } to transaction
			//update transaction
			add { otp -> transaction } to PendingTransactions
		endseq
		else seq
			//finally performing transfer by appending to the current ledger
			Append(Transaction(transaction, client, "debit", now), Ledger)
			
			//update state
			add { "state" -> TRANSACTION_PERFORMED } to transaction
			//update transaction
			add { otp -> transaction } to PendingTransactions

			limitAlert := CheckAccountLimitsAlerts(
				profileOf(ownerOf(fromA)),
				accountOf(fromA))
		
			if (limitAlert = undef) then
				do_log("No limit alert", DEBUG)
			else
				do_log("Limit alert: " + limitAlert, WARN)
			
			//remove otp before sending to creditor/debitor
			remove {"otp" -> otp} from transaction
			
			//asim bug: first message not sent
			//send success message, attach limitAlert if necessary
			Send(TransferPerformedSuccessfully(transaction, limitAlert), client) //client=debitor=buyer
			Send(TransferPerformedSuccessfully(transaction, undef), //do not attach limit alerts to creditor
					activeClient(creditorOf(transaction))) //send to creditor
			skip //dummy
		endseq
	endseq
endseq

//-----------------------------------------------------------------------------------------------------------
//				begin main programm
//-----------------------------------------------------------------------------------------------------------
rule DispatchMessages = {
	forall m in inboxOf(self) do seq
		//fetch message information
		msubject := getMessageSubject(m)
		msgIn := getMessageContent(m)
		member := getMessageSender(m)
		
		do_log("message received from " + member + " with subject " + msubject , DEBUG)
		
		//dispatch messages
    	if msgIn != undef and member != undef then
		case msubject of
			toString(CreditPreviewReq): HandleCreditPreviewReq(msgIn, member)
			toString(CreditPerformReq): HandleCreditPerformReq(msgIn, member)
			toString(DebitPreviewReq): HandleDebitPreviewReq(msgIn, member)
			toString(DebitPerformReq): HandleDebitPerformReq(msgIn, member)
			toString(DebitAckCompletion): HandleDebitAckCompletion(msgIn, member)
		endcase
    endseq
}