/*includeskip begin*/
CoreASIM DebitRequestClient
use Standard 
init dummy
rule dummy = skip
scheduling NoPolicy
policy NoPolicy = skip

// ------------------------------- !!important!! -------------------------------
// Global definitions are NOT used when instantiating a client "dynamically"
// because "createASIM" function only allows to provide init, program and policy.
// Thus globally used variables are defined by their first usage.
// These definitions here are just placed to avoid warnings in eclipse
// ------------------------------- !!important!! -------------------------------
controlled messageNext: SET
controlled client_state: STRING
controlled DEBITREQUEST: MAP

controlled DebitPreviewReq: STRING
controlled DebitPerformReq: STRING
controlled DebitAckExec: STRING
controlled DoNotProceed: STRING
controlled Proceed: STRING
controlled TransferPerformed: STRING

controlled RECEIVE: STRING
controlled SEND: STRING
controlled TERMINATE: STRING
controlled DONE: STRING
// ------------------------^^^^^^^ read important ^^^^^^^------------------------ 
/*includeskip end*/

policy SkipDebitRequestClient = skip

rule InitDebitRequestClient = seq
	//cannot be done facilitating universe definition
	//see !!important!! comment on top. thus here are
	//"pseudo"-static definition used
	DebitPreviewReq := "DebitPreviewReq"
	DebitPerformReq := "DebitPerformReq"
	DebitAckExec := "DebitAckExec" //TODO: handle with second "debit" client
	Proceed := "Proceed"
	DoNotProceed := "DoNotProceed"
	TransferPerformed := "TransferPerformed"
	
	RECEIVE := "RECEIVE"
	SEND := "SEND"
	TERMINATE := "TERMINATE"
	DONE := "DONE"
	
	//init variables
	messageNext := { DebitPreviewReq }
	client_state := SEND
	DEBITREQUEST := {
					"debitor" -> "mbrId4",
					"channel" -> "Service",
					//"amount"  -> 40, //instant transfer
					"amount" -> 2000, //needs acknowledgement	
					"metadata" -> {"message", "some debit transfer"}
				}
				
	program(self) := ProgramDebitRequestClient
endseq

rule ProgramDebitRequestClient = local msgSubject, message, mbr in par
	//print "client_state:: " + client_state + "; messageNext:: " + messageNext + "; DebitPreviewReq:: " + DebitPreviewReq
	
	//send initial request
	if (DebitPreviewReq memberof messageNext) = true and client_state = SEND then par
		messageNext := { DoNotProceed, Proceed}
		client_state := RECEIVE
		send DEBITREQUEST  to "server" with subject DebitPreviewReq		
	endpar
	else
	// check for Proceed message
	if client_state = RECEIVE then par
		forall m in inboxOf(self) do seq
			//fetch message data
			//maybe message contains some kind of signature in production
			//which needs to be checked			
			msgSubject := getMessageSubject(m)
			message := getMessageContent(m) 
			mbr := getMessageSender(m)
			
			//handle messages - only if they are from the server
			if (mbr = "server@server") then seq
				if msgSubject = Proceed then seq
					print "sending message DebitPerformReq to server"
					send DEBITREQUEST  to "server" with subject DebitPerformReq
					messageNext := { TransferPerformed, DoNotProceed }					
				endseq
				else if msgSubject = DoNotProceed then seq
					print "cannot proceed with request: " + message
					client_state := TERMINATE
				endseq
				else if msgSubject = TransferPerformed then seq
					client_state := TERMINATE
					print "DRC Transfer client_state:: " + client_state + "; message:: " + message
				endseq
				else seq
					print "unknown message: "+ message + ", stopped listenting."
					client_state := TERMINATE
				endseq
			endseq
		endseq
	endpar
	else
	//send done message
	if client_state = TERMINATE then par
		//"destroyASIM self" not working
		//destroyASIM <name> would work but I don't know how to determine the name of the current ASIM
		//=> send done message to scheduler which ends the asim by retrieving the sender name
		send "Done" to "scheduler" with subject "Done"
		client_state := DONE
	endpar
endpar